plugins {
    id 'com.android.application'
}

def ServerMode = "michael.linker.rewater.config.BuildConfiguration.Server"
def ServerProtocol = "michael.linker.rewater.config.BuildConfiguration.ServerProtocol"
def BluetoothMode = "michael.linker.rewater.config.BuildConfiguration.Bluetooth"
def PingAddress = "\"/google.com\""
// Dev
def AndroidDevServerAddress = "\"/10.0.2.2\""
def DevServerAddress = "\"/192.168.0.3\""
// Prod
def ProdServerAddress = "\"/rewater.local\""

android {
    compileSdk 32

    defaultConfig {
        applicationId "michael.linker.rewater"
        minSdk 29
        targetSdk 32
        versionCode 193
        versionName "0.5.193"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "Boolean", "HEALTHCHECK_INTERNET", "true"
            buildConfigField "Boolean", "HEALTHCHECK_SERVER", "false"
            buildConfigField ServerMode, "SERVER_MODE", "${ServerMode}.LOCAL"
            buildConfigField ServerProtocol, "SERVER_PROTOCOL", "${ServerProtocol}.HTTP"
            buildConfigField "String", "SERVER_ADDRESS", AndroidDevServerAddress
            buildConfigField "String", "PING_ADDRESS", PingAddress
            buildConfigField BluetoothMode, "BLUETOOTH_MODE", "${BluetoothMode}.STUB"
        }
        release {
            buildConfigField "Boolean", "HEALTHCHECK_INTERNET", "true"
            buildConfigField "Boolean", "HEALTHCHECK_SERVER", "true"
            buildConfigField ServerMode, "SERVER_MODE", "${ServerMode}.GLOBAL"
            buildConfigField ServerProtocol, "SERVER_PROTOCOL", "${ServerProtocol}.HTTP"
            buildConfigField "String", "SERVER_ADDRESS", ProdServerAddress
            buildConfigField "String", "PING_ADDRESS", PingAddress
            buildConfigField BluetoothMode, "BLUETOOTH_MODE", "${BluetoothMode}.PROD"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }

    sourceSets {
        main {
            res.srcDirs =
                    [

                            /* Layouts */
                            'src/main/res/layouts/activity',
                            'src/main/res/layouts/common',
                            'src/main/res/layouts/view',
                            'src/main/res/layouts/fragment',
                            /* General folder */
                            'src/main/res'
                    ]
        }
    }
}

dependencies {
    def room_version = "2.4.3"
    def rxandroidble_version = "1.17.2"
    def client_bluetooth_version = "1.3.9"
    def gson_version = "2.10"
    def ok_http_version = "4.10.0"
    def mp_android_chart_version = "v3.1.0"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation "com.google.code.gson:gson:$gson_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation "io.reactivex.rxjava3:rxjava:3.1.5"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation "com.polidea.rxandroidble3:rxandroidble:$rxandroidble_version"
    implementation "me.aflak.libraries:bluetooth:$client_bluetooth_version"
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.github.PhilJay:MPAndroidChart:$mp_android_chart_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}